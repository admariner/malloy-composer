//
// Basic names analysis.
//
//  Table has 5 columns name, state, year, gender and number (population)
//
//  number of people in that state, year, gender born with that name

//
// Malloy language Documentation
//  see: https://malloydata.github.io/documentation/
//

// source, see: https://malloydata.github.io/documentation/language/source.html
source: names is duckdb.table('usa_names.parquet') extend {


  measure: population is `number`.sum()
  dimension: decade is floor(`year`/10)*10


  // view, see: https://malloydata.github.io/documentation/language/query.html
  view: by_name is {
    group_by: name
    aggregate: population
    limit: 10
  }

  view: by_state is {
    group_by: state
    aggregate: population
  }

  view: by_gender is {
    group_by: gender
    aggregate: population
  }

  view: by_year is {
    group_by: `year`
    aggregate: population
    order_by: 1 asc
  }

  view: by_decade is {
    group_by: decade
    aggregate: population
    order_by: 1 asc
  }

  // filters, see: https://malloydata.github.io/documentation/language/filters.html
  view: male_names is by_name + {where: gender = 'M'}
  view: female_names is by_name + {where: gender = 'F'}

  view: top_names_by_state_ea_gender is by_state + {
    nest: male_names
    nest: female_names
    limit: 10
  }

  view: name_dashboard is by_name + {
    nest:
      by_decade
      by_state
    limit: 10
  }
}

// named queries
//  see: https://malloydata.github.io/documentation/language/view.html
query: j_names is names -> name_dashboard + {where: name ~ r'J'}
